[Actions]
; common Values for (un)installation
include_insert "common.opsiinc"

if not(HasMinimumSpace ("%SystemDrive%", $MinimumSpace$))
	LogError "Not enough space on %SystemDrive%, " + $MinimumSpace$ + " on drive %SystemDrive% needed for " + $ProductId$
	isFatalError
endif

ChangeDirectory "%ScriptPath%"

if not( FileExists($Setupfile$) )
	LogError "Setupfile NOT exists (" + $Setupfile$ +")"
	isFatalError "No Setupfile found"
endif 

if FileExists("%ScriptPath%\delsub.opsiscript")
	comment "Start uninstall sub section"
	Sub "%ScriptPath%\delsub.opsiscript"
endif
   
Message "Installing %installingProdName% (%installingProdVersion%) ..."

comment "Start setup program"
Winbatch_install_inno
Set $ExitCode$ = getLastExitCode
if stringToBool(isInnoExitcodeFatal($ExitCode$, "True", $ErrorString$))
        isFatalError
endif

Set $ResultList$ = SearchRegistryUninstallKeys($ProductId$)

if (count($ResultList$) = "0")
	logError "Fatal: After Installation " + $ProductName$ + " could not be found in Registry"
	isFatalError
else
	comment "Successful Installation"

	;sollte nur einen Treffer, daher nur erste Zeile nehmen
	Set $RegId$ = takeString (0,splitString(takeString(0,$ResultList$), '|'))
	Set $RegBitScope$ = takeString (1,splitString(takeString(0,$ResultList$), '|'))

	; check version
	Set $InstalledVersion$ = GetRegistryValue($RegId$, "DisplayVersion", $RegBitScope$)
	if not ($ProductVersion$ = $InstalledVersion$)
		logError "Fatal: Versions (package <-> installed) does not match)"
		isFatalError
	endif

	comment "Get 'DisplayIcon' from registry for path to exe"
	Set $PathToExe$ = GetRegistryValue($RegId$, "DisplayIcon", $RegBitScope$)

	if $DesktopLink$ = "true"
		comment "Create common desktop link"
		Linkfolder_create_desktoplink	
	endif

	comment "include custom post install file"
	set $CustomPostInstall$ = getProductProperty("custom-post-install","none")
	if not ($CustomPostInstall$ = "none")
		if FileExists("%ScriptPath%\custom\" + $CustomPostInstall$)
			include_insert "%ScriptPath%\custom\" + $CustomPostInstall$
		endif
	endif

endif

[Winbatch_install_inno]
; http://unattended.sourceforge.net/InnoSetup_Switches_ExitCodes.html
; You may create setup answer file by: setup.exe /SAVEINF="filename"
; You may use an answer file by the parameter /LOADINF="filename"
; bei diesem Installer funktioniert die Technik mit einem "Answer file" nicht!!! es wird immer Type=full installiert, mit Parametern funktioniert es
; siehe http://www.jrsoftware.org/ishelp/index.php?topic=setupcmdline
"%ScriptPath%\$Setupfile$" /sp- $SilentOption$ /norestart /nocancel /SUPPRESSMSGBOXES /TYPE="custom" /TASKS="sendtohook" /COMPONENTS="main,shellext,pageant,puttygen,transl"

[Linkfolder_create_desktoplink]
set_basefolder common_desktopdirectory
set_link
	name: $ProductName$
	target: $PathToExe$
end_link
